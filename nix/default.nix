{
  config,
  lib,
  pkgs,
  ...
}: let
  cfg = config.cadair.emacs;
in {
  options.cadair.emacs = {
    enable = lib.mkEnableOption "Cadair's emacs";
    emacs-package = lib.mkPackageOption pkgs "emacs30-pgtk" { };
  };

  config = lib.mkIf cfg.enable {
    home.packages = with pkgs; [
      fira-code
      fira-code-nerdfont
      fira-code-symbols
      jetbrains-mono
      git
      ripgrep
      fd
      emacs-all-the-icons-fonts
      # lsp
      python312Packages.python-lsp-server
      python312Packages.pyls-isort
      python312Packages.ruff
      # spelling
      ispell
      # nix lsp
      nil
      # dap
      python312Packages.debugpy
    ];

    # emacs
    programs.emacs = {
      enable = true;
      package = cfg.emacs-package;
      extraPackages = epkgs: [ epkgs.tree-sitter epkgs.tree-sitter-langs epkgs.treesit-grammars.with-all-grammars epkgs.nerd-icons ];
      # Copied from https://github.com/danth/stylix/blob/master/modules/emacs/hm.nix#L69
      # To customise font config
      extraConfig = with config.stylix; lib.mkForce ''
            ;; ---- Generated by stylix ----
            (require 'base16-stylix-theme)
            (setq base16-theme-256-color-source 'colors)
            (load-theme 'base16-stylix t)
            ;; Set font
            (set-face-attribute 'default nil :font (font-spec :family "${fonts.monospace.name}" :size ${builtins.toString (fonts.sizes.terminal * 1.0)}))
            (set-face-attribute 'fixed-pitch nil :family "${fonts.monospace.name}")
            (set-face-attribute 'variable-pitch nil :font (font-spec :family "${fonts.sansSerif.name}" :size ${builtins.toString (fonts.sizes.terminal * 1.1)}))
            ;; -----------------------------
        '';
    };

    home.file.emacs-init = {
      source = "../init.el";
      target = ".emacs.d/init.el";
    };

    home.file.emacs-local-packages = {
      source = "../local-packages";
      target = ".emacs.d/local-packages/";
      recursive = true;
    };

    home.file.emacs-capture = {
      target = ".config/bin/emacs-capture";
      executable = true;
      text = ''
        #!/bin/sh
        # Setup info here: http://www.mediaonfire.com/blog/2017_07_21_org_protocol_firefox.html
        ${cfg.emacs-package.out}/bin/emacsclient -c -F "((name . \"emacs-capture\") (title . \"emacs-capture\") (height . 15) (width . 110))" "$@" &
        '';
    };

    # Write a custom emacsd
    systemd.user.services.emacsd = {
      Unit = {
        Description = "Emacs: the extensible, self-documenting text editor";
        After = ["graphical-session.target"];
        Requires = ["ssh-agent.service"];
      };

      Service = {
        Environment = ["SSH_AUTH_SOCK=%t/keyring/ssh"];
        EnvironmentFile = "${config.home.homeDirectory}/${config.home.file.session_env.target}";
        Type = "forking";
        ExecStart = "${cfg.emacs-package.out}/bin/emacs --daemon";
        ExecStop = "${cfg.emacs-package.out}/bin/emacsclient --eval (kill-emacs)";
        Restart = "always";
      };

      Install = {
        WantedBy = ["graphical-session.target" "sway-session.target"];
      };
    };

    systemd.user.services.emacs-todo = {
      Unit = {
        Description = "Emacs Agenda Window";
        After = ["emacsd.service"];
        Requires = ["emacsd.service"];
      };

      Service = {
        Type = "simple";
        ExecStartPre = "${pkgs.coreutils.out}/bin/sleep 10";
        ExecStart = ''${cfg.emacs-package.out}/bin/emacsclient -c -F "((title . \"emacs-todo\") (name . \"emacs-todo\") (height . 65) (width . 260))" --eval '(org-agenda)' '';
        Restart = "on-failure";
      };

      Install = {
        WantedBy = ["sway-session.target"];
      };
    };

    home.file.waybar-get-org-task = {
      target = ".config/waybar/get_org_task.sh";
      executable = true;
      text = ''
        #!/bin/sh

        ${config.lib.shell.exportAll config.home.sessionVariables}

        json=$(${cfg.emacs-package.out}/bin/emacsclient --eval '(org-clock-waybar-output-task)' 2> /dev/null)
        status=$?
        [ $status -eq 0 ] && echo $(echo $json | ${pkgs.jq}/bin/jq fromjson --unbuffered --compact-output) || echo ""
    '';
    };
  };
}
